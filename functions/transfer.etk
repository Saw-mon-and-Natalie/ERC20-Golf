# stack: return_pc
transfer:                                       # stack after opcode is executed
    jumpdest

    %calldata_param(1)                          # amount, return_pc
    %calldata_param(0)                          # to, amount, return_pc

    caller
    %push(transferInternal)
    jump

# stack: from, to, amount, return_pc
transferInternal:
    jumpdest

    %push(transferInternalPart1)
    dup2
    %push(balanceOfSlot) 
    jump                         
    
# f.sid, from, to, amount, return_pc
transferInternalPart1:
    jumpdest
    dup4                                        # amount, f.sid, from, to, amount, return_pc

    # balanceOf[from] 
    dup2
    sload                                       # balanceOf[from], amount, f.sid, from, to, amount, return_pc

    dup2                                        # amount, balanceOf[from], amount, f.sid, from, to, amount, return_pc
    dup2                                        # balanceOf[from], amount, balanceOf[from], amount, f.sid, from, to, amount, return_pc
    
    lt
    %push(error)
    jumpi                                       # balanceOf[from], amount, f.sid, from, to, amount, return_pc

    sub                                         # balanceOf[from] - amount, f.sid, from, to, amount, return_pc
    dup2                                        # f.sid, balanceOf[from] - amount, f.sid, from, to, amount, return_pc
    sstore                                      # f.sid, from, to, amount, return_pc
                                    
    %push(transferInternalPart2)                # xxxx, f.sid, from, to, amount, return_pc
    dup4                                        # to, xxxx, f.sid, from, to, amount, return_pc
    %push(balanceOfSlot) 
    jump                          
    
# to.sid, f.sid, from, to, amount, return_pc
transferInternalPart2:
    jumpdest

    dup1                                        # to.sid, to.sid, f.sid, from, to, amount, return_pc
    sload                                       # balanceOf[to], to.sid, f.sid, from, to, amount, return_pc

    dup6                                        # amount, balanceOf[to], to.sid, f.sid, from, to, amount, return_pc
    add                                         # amount + balanceOf[to], to.sid, f.sid, from, to, amount, return_pc

    swap1
    sstore                                      # f.sid, from, to, amount, return_pc

    swap3                                       # amount, from, to, f.sid, return_pc
    returndatasize
    mstore                                      # from, to, f.sid, return_pc

    # emit Transfer(address indexed _from, address indexed _to, uint256 _value);
    push32 topic("Transfer(address,address,uint256)")
    push1 0x20
    returndatasize
    log3                                        # f.sid, return_pc

    push1 1
    %push(returnValue)
    jump

